

CREATE VIEW [AM].[v_MASTER_QUEUE]
AS 

WITH
BATCHARTIFACT AS 
	(
								
	SELECT ART_CTRL_MASTER_ID FROM AM.ARTIFACT_CTRL_MASTER (NOLOCK) 
	WHERE 
	
	LAST_STATUS_CODE_VALUE_ID IN (0,-1)
				
	)
,
EXECUTABLEARTIFACT AS
	(
	SELECT
		 ACM.ART_CTRL_MASTER_ID
		,ACM.LAST_LOAD_ID
		,ACM.SCHEDULE_MODE_VALUE_ID
		,ACM.SCHEDULE_TYPE_VALUE_ID
		,ACM.ART_GROUP_VALUE_ID               			
		,ACM.ART_LAYER_VALUE_ID
		,ACM.ART_SCALE_VALUE
		,ACM.LAST_STATUS_CODE_VALUE_ID
		,ACM.NEXT_PROCESS_TYPE_VALUE_ID
		,ACM.NEXT_PROCESS_DATE_TIME
		,ACM.ART_BATCH_RETRY_COUNTER
		,ACM.ART_BATCH_RETRY_THRESHOLD
		,ACM.IS_ENABLED_IND
		,S.PRIORITY_VALUE AS 'SCHEDULE_PRIORITY'
		,G.PRIORITY_VALUE AS 'GROUP_PRIORITY'
		,L.PRIORITY_VALUE AS 'LAYER_PRIORITY'
	FROM
	AM.ARTIFACT_CTRL_MASTER (NOLOCK) ACM                          
	INNER JOIN BATCHARTIFACT BR on ACM.ART_CTRL_MASTER_ID = BR.ART_CTRL_MASTER_ID
	INNER JOIN AM.ARTIFACT_CODE_VALUE S ON ACM.SCHEDULE_TYPE_VALUE_ID = S.CODE_VALUE_ID
	INNER JOIN AM.ARTIFACT_CODE_VALUE G ON ACM.ART_GROUP_VALUE_ID = G.CODE_VALUE_ID
	INNER JOIN AM.ARTIFACT_CODE_VALUE L ON ACM.ART_LAYER_VALUE_ID = L.CODE_VALUE_ID


	WHERE SCHEDULE_MODE_VALUE_ID <> 54
			and ART_TYPE_VALUE_ID IN(21,23)
			and IS_ENABLED_IND = 1  				
					                               
	)    
				
-- ***************  MANUAL OVERRIDE WITHOUT CHECKING DEPENDENCIES ******************

	SELECT 
            EX.ART_CTRL_MASTER_ID   
		,EX.LAST_LOAD_ID
		,EX.SCHEDULE_MODE_VALUE_ID                                
        ,AD.ART_DEPLOYMENT_PATH							
        ,ART_GROUP_VALUE_ID                                               
        ,ART_SCALE_VALUE
        ,ART_LAYER_VALUE_ID
        ,LAST_STATUS_CODE_VALUE_ID
		,EX.NEXT_PROCESS_TYPE_VALUE_ID
		,EX.NEXT_PROCESS_DATE_TIME
        ,1 'MODE_PRIORITY'
		,EX.SCHEDULE_PRIORITY
		,EX.GROUP_PRIORITY
		,EX.LAYER_PRIORITY
		,EX.IS_ENABLED_IND
							
	FROM
        EXECUTABLEARTIFACT EX
		JOIN AM.ARTIFACT_DEPLOYMENT AD
			ON EX.ART_CTRL_MASTER_ID = AD.ART_CTRL_MASTER_ID												
        WHERE NEXT_PROCESS_TYPE_VALUE_ID = 63		-- ARTIFACT NEXT PROCESS TYPE VALUE IS MANUAL OVERRIDE W/O DEPENDENCIES
			AND LAST_STATUS_CODE_VALUE_ID = 0		-- INITIALIZED VIA PROCEDURE, NOT -1 No retries for manual                                 
			AND ISNULL(ART_BATCH_RETRY_COUNTER,0)	-- CHECK THRESHOLD            
				< ART_BATCH_RETRY_THRESHOLD 

			-- ***************  MANUAL OVERRIDE WITH CHECKING DEPENDENCIES ******************
	UNION ALL

		SELECT 
				EX.ART_CTRL_MASTER_ID  
			,EX.LAST_LOAD_ID
			,EX.SCHEDULE_MODE_VALUE_ID                                    
			,AD.ART_DEPLOYMENT_PATH							
			,ART_GROUP_VALUE_ID                                               
			,ART_SCALE_VALUE
			,ART_LAYER_VALUE_ID
			,LAST_STATUS_CODE_VALUE_ID
			,EX.NEXT_PROCESS_TYPE_VALUE_ID
			,EX.NEXT_PROCESS_DATE_TIME
			,2 'MODE_PRIORITY'
			,EX.SCHEDULE_PRIORITY
			,EX.GROUP_PRIORITY
			,EX.LAYER_PRIORITY
			,EX.IS_ENABLED_IND
		FROM
			EXECUTABLEARTIFACT EX
			JOIN AM.ARTIFACT_DEPLOYMENT AD
				ON EX.ART_CTRL_MASTER_ID = AD.ART_CTRL_MASTER_ID												
			WHERE NEXT_PROCESS_TYPE_VALUE_ID = 62		-- ARTIFACT NEXT PROCESS TYPE VALUE IS MANUAL OVERRIDE WITH DEPENDENCIES
				AND LAST_STATUS_CODE_VALUE_ID = 0		-- INITIALIZED VIA PROCEDURE, NOT -1 No retries for manual                                  
				AND ISNULL(ART_BATCH_RETRY_COUNTER,0)	-- CHECK THRESHOLD            
					< ART_BATCH_RETRY_THRESHOLD 
			--NOW CHECK DEPENDENCIES 
            AND 2 = all
            (
				SELECT 
				distinct ACM1.LAST_STATUS_CODE_VALUE_ID 
				FROM AM.ARTIFACT_DEPENDENCY AD
				JOIN AM.ARTIFACT_CTRL_MASTER ACM1
				ON ACM1.ART_CTRL_MASTER_ID = AD.REFERENCED_CTRL_MASTER_ID
				WHERE EX.ART_CTRL_MASTER_ID = AD.ART_CTRL_MASTER_ID                                            
				AND AD.DEPENDENCY_TYPE_VALUE_ID = 71 -- ONLY WITH BATCH DEPENDENCY 	
			)

		UNION ALL
			--************* Batch Inizalized (Always with Dependencies)  *****************************
			-- SELECT ALL ARTIFACTS WHICH WERE INTILIAZED BY THE BATCH INITIALIZED SCRIPT.
			-- AND WITH NEXT PROCESS DATE TIME <= CURRENT DATE TIME. 
			-- ALL PACKAGES MUST ALWAYS CHECK FOR DEPENDENCIES.
        SELECT DISTINCT
				EX.ART_CTRL_MASTER_ID                                   
			,EX.LAST_LOAD_ID
			,EX.SCHEDULE_MODE_VALUE_ID                                    
			,ADP.ART_DEPLOYMENT_PATH							
			,ART_GROUP_VALUE_ID                                               
			,ART_SCALE_VALUE
			,ART_LAYER_VALUE_ID
			,LAST_STATUS_CODE_VALUE_ID
			,EX.NEXT_PROCESS_TYPE_VALUE_ID
			,EX.NEXT_PROCESS_DATE_TIME
			,3 'MODE_PRIORITY'
			,EX.SCHEDULE_PRIORITY
			,EX.GROUP_PRIORITY
			,EX.LAYER_PRIORITY
			,EX.IS_ENABLED_IND
		FROM
			EXECUTABLEARTIFACT EX  
            JOIN AM.ARTIFACT_DEPLOYMENT ADP
				ON EX.ART_CTRL_MASTER_ID = ADP.ART_CTRL_MASTER_ID
			join AM.ARTIFACT_DEPENDENCY AD
				on EX.ART_CTRL_MASTER_ID = AD.ART_CTRL_MASTER_ID
            WHERE EX.NEXT_PROCESS_TYPE_VALUE_ID = 61		-- ARTIFACT IS PART OF BATCH
				AND EX.LAST_STATUS_CODE_VALUE_ID IN (-1,0)	-- ARTIFACT WAS INITIALIZED BY THE BATCH INITIALIZATION SCRIPT AND IS READY TO RUN
				AND EX.NEXT_PROCESS_DATE_TIME <= GETDATE()	-- NEXT PROCESS DATE IS LESS THAN CURRENT DATE TIME
                AND ISNULL(EX.ART_BATCH_RETRY_COUNTER,0)	-- CHECK THRESHOLD  
					< EX.ART_BATCH_RETRY_THRESHOLD                                                                    
            --NOW CHECK DEPENDENCIES 
            AND 2 = all
            (
				SELECT 
				distinct ACM1.LAST_STATUS_CODE_VALUE_ID 
				FROM AM.ARTIFACT_DEPENDENCY AD
				JOIN AM.ARTIFACT_CTRL_MASTER ACM1
				ON ACM1.ART_CTRL_MASTER_ID = AD.REFERENCED_CTRL_MASTER_ID
				WHERE EX.ART_CTRL_MASTER_ID = AD.ART_CTRL_MASTER_ID                                            
				AND AD.DEPENDENCY_TYPE_VALUE_ID = 71 -- ONLY WITH BATCH DEPENDENCY 										
            )

		UNION ALL
			--************* Batch Inizalized (Without Dependencies)  *****************************
			-- SELECT ALL ARTIFACTS WHICH WERE INTILIAZED BY THE BATCH INITIALIZED SCRIPT.
			-- AND WITH NEXT PROCESS DATE TIME <= CURRENT DATE TIME. 
			-- ALL PACKAGES MUST ALWAYS CHECK FOR DEPENDENCIES.
        SELECT DISTINCT
				EX.ART_CTRL_MASTER_ID                                   
			,EX.LAST_LOAD_ID
			,EX.SCHEDULE_MODE_VALUE_ID                                    
			,ADP.ART_DEPLOYMENT_PATH							
			,ART_GROUP_VALUE_ID                                               
			,ART_SCALE_VALUE
			,ART_LAYER_VALUE_ID
			,LAST_STATUS_CODE_VALUE_ID
			,EX.NEXT_PROCESS_TYPE_VALUE_ID
			,EX.NEXT_PROCESS_DATE_TIME
			,3 'MODE_PRIORITY'
			,EX.SCHEDULE_PRIORITY
			,EX.GROUP_PRIORITY
			,EX.LAYER_PRIORITY
			,EX.IS_ENABLED_IND
		FROM
			EXECUTABLEARTIFACT EX  
            JOIN AM.ARTIFACT_DEPLOYMENT ADP
				ON EX.ART_CTRL_MASTER_ID = ADP.ART_CTRL_MASTER_ID
			-- BELOW JOIN WILL RETURN ALL ARTIFACTS WHICH DO NOT HAVE ANY DEPENDENCIES
            -- NOW CHECK NO DEPENDENCIES EXISTS
            AND not exists
            (
                SELECT 
                LAST_STATUS_CODE_VALUE_ID
                FROM 
                AM.ARTIFACT_DEPENDENCY AD                                           
                WHERE EX.ART_CTRL_MASTER_ID = AD.ART_CTRL_MASTER_ID                                            
                AND AD.DEPENDENCY_TYPE_VALUE_ID = 71 -- ONLY WITH BATCH DEPENDENCY                                                    
            )
            WHERE EX.NEXT_PROCESS_TYPE_VALUE_ID = 61		-- ARTIFACT IS PART OF BATCH
				AND EX.LAST_STATUS_CODE_VALUE_ID IN (-1,0)	-- ARTIFACT WAS INITIALIZED BY THE BATCH INITIALIZATION SCRIPT AND IS READY TO RUN
				AND EX.NEXT_PROCESS_DATE_TIME <= GETDATE()	-- NEXT PROCESS DATE IS LESS THAN CURRENT DATE TIME
                AND ISNULL(EX.ART_BATCH_RETRY_COUNTER,0)	-- CHECK THRESHOLD  
					< EX.ART_BATCH_RETRY_THRESHOLD                   








